
Функция СведенияОВнешнейОбработке() Экспорт
  
  ПараметрыРегистрации = Новый Структура;
  ПараметрыРегистрации.Вставить("Вид", "ЗаполнениеОбъекта"); //Варианты: "ДополнительнаяОбработка", "ДополнительныйОтчет", "ЗаполнениеОбъекта", "Отчет", "ПечатнаяФорма", "СозданиеСвязанныхОбъектов" 
  
  МассивНазначений = Новый Массив();
  МассивНазначений.Добавить("Справочник.Контрагенты");
  
  ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
  
  ПараметрыРегистрации.Вставить("Наименование", 	"Отправка рассылок в telegram chat");
  ПараметрыРегистрации.Вставить("Версия", 			"1.0"); 
  ПараметрыРегистрации.Вставить("БезопасныйРежим", 	Ложь); 
  ПараметрыРегистрации.Вставить("Информация", 		"Формирование текста рассылки в telegram chat");
  ПараметрыРегистрации.Вставить("ВерсияБСП", 		"2.2.1.4");

  ТаблицаКоманд = ПолучитьТаблицуКоманд();

  ДобавитьКоманду(ТаблицаКоманд,
          "Отправить в telegram chat",
          "СформироватьИОтправитьВТелеграмм",
          "ОткрытиеФормы",     
          Истина, 
          ""); 

  ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);

  Возврат ПараметрыРегистрации;

КонецФункции

Функция ПолучитьТаблицуКоманд()

  Команды = Новый ТаблицаЗначений;
  Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
  Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
  Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
  Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
  Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));

  Возврат Команды;

КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")

  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

#Область Вспомогательные

Функция ПолучитьИдентификаторПолучателя(Знач ОбъектНазначения) 
	     
	МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
	СписокСвойствИДополнительныхРеквизитов = МодульУправлениеСвойствами.ПолучитьСписокСвойств(ОбъектНазначения, Истина, Ложь);
	ИдентификаторЧата = Неопределено;
	
	Для каждого ДополнительныйРеквизит Из СписокСвойствИДополнительныхРеквизитов Цикл		
		Если ТипЗнч(ДополнительныйРеквизит) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения")
			 И СтрНайти(ВРег(ДополнительныйРеквизит.Имя),ВРег("CHAT_ID")) > 0 Тогда			 
			ИдентификаторЧата = ДополнительныйРеквизит;
			Прервать;			
		КонецЕсли;   		
	КонецЦикла; 
	
	Если ИдентификаторЧата = Неопределено Тогда	
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"У %1 не установлен дополнительный строковый реквизит CHAT_ID", ОбъектНазначения);		
	КонецЕсли; 
	
	ТаблицаЗначенийСвойства = МодульУправлениеСвойствами.ПолучитьЗначенияСвойств(
		ОбъектНазначения, 
		Истина, 
		Ложь, 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЧата));
		
	Если Не ТаблицаЗначенийСвойства.Количество() Тогда
		ВызватьИсключение нСтр("ru = 'Неожиданные проблемы с получением значения идентификатора чата'");
	КонецЕсли; 
	
	Возврат Формат(ТаблицаЗначенийСвойства[0].Значение,"ЧДЦ=0; ЧГ=");	
	                                    
КонецФункции //ПолучитьИдентификаторПолучателя

Функция ОтправитьСообщение(Знач ОбъектНазначения, Знач ТексСообщения) Экспорт 
	
	// https://gist.github.com/PlugFox/4499132c45b73ae5bc8dda52a1218dff
	Соединение  = ПолучитьСоединение();
	Адрес       = СтрШаблон("/bot%1/sendMessage?chat_id=%2&parse_mode=HTML&text=%3"
                , ПолучитьAPIToken()
                , ПолучитьИдентификаторПолучателя(ОбъектНазначения)
                , КодироватьСтроку(ТексСообщения, СпособКодированияСтроки.КодировкаURL, "UTF8"));
	Заголовки   = Новый Соответствие;
    Запрос      = Новый HTTPЗапрос(Адрес, Заголовки);
    
    // GET
    Ответ       = Соединение.Получить(Запрос);
    
    // Разбор ответа
    Если Ответ.КодСостояния <> 200 Тогда
		
		ОтветСтрокой    = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		
        ВызватьИсключение СтрШаблон("Ошибка отправки вложения в telegram.
            |Код состояния: %1
            |Тело: %2"
            , Ответ.КодСостояния
            , ОтветСтрокой
        ); 
    КонецЕсли;				
	
КонецФункции //ОтправитьСообщение


Функция ПолучитьAPIToken()
	
	СтрокаJSON = ПолучитьМакет("ДанныеАвторизации").ПолучитьТекст();
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	СтруктураПараметров = ПрочитатьJSON(ЧтениеJSON,Ложь);
	ЧтениеJSON.Закрыть();
	
	Возврат СтруктураПараметров.APItoken;
	
КонецФункции //ПолучитьAPIToken

Функция ПолучитьСоединение()    
	
	SSL         = Новый ЗащищенноеСоединениеOpenSSL();
    Соединение  = Новый HTTPСоединение("api.telegram.org", 443,,,, 30, SSL, Ложь); 
    Возврат Соединение;
	
КонецФункции // ПолучитьСоединение()


#КонецОбласти