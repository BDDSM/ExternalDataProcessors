
//ПОДГОТОВКА РЕГИСТРАЦИИ ОБРАБОТКИ

Функция СведенияОВнешнейОбработке() Экспорт
  
  ПараметрыРегистрации = Новый Структура;
  ПараметрыРегистрации.Вставить("Вид", "СозданиеСвязанныхОбъектов"); //Варианты: "ДополнительнаяОбработка", "ДополнительныйОтчет", "ЗаполнениеОбъекта", "Отчет", "ПечатнаяФорма", "СозданиеСвязанныхОбъектов" 
  
  МассивНазначений = Новый Массив();
  МассивНазначений.Добавить("Документ.ОтчетКомиссионераОПродажах");
  МассивНазначений.Добавить("Документ.ОтчетОРозничныхПродажах");
  
  ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
  
  ПараметрыРегистрации.Вставить("Наименование", "Создание связанного отчета производства за смену на основании отчета комиссионеру");
  ПараметрыРегистрации.Вставить("Версия", "1.2"); //"1.0"
  ПараметрыРегистрации.Вставить("БезопасныйРежим", Истина); //Варианты: Истина, Ложь
  ПараметрыРегистрации.Вставить("Информация", "");
  ПараметрыРегистрации.Вставить("ВерсияБСП", "3.0.1.414");// не ниже какой версии БСП подерживается обработка

  ТаблицаКоманд = ПолучитьТаблицуКоманд();

  ДобавитьКоманду(ТаблицаКоманд,
          "Отчет производства за смену",
          "ОтчетПроизводстваЗаСмену",
          "ВызовСерверногоМетода",  //Использование.  Варианты: "ОткрытиеФормы", "ВызовКлиентскогоМетода", "ВызовСерверногоМетода"   
          Истина,//Показывать оповещение. Варианты Истина, Ложь 
          "");//Модификатор 

  ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);

  Возврат ПараметрыРегистрации;

КонецФункции

Функция ПолучитьТаблицуКоманд()

  Команды = Новый ТаблицаЗначений;
  Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
  Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
  Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
  Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
  Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));

  Возврат Команды;

КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")

  НоваяКоманда = ТаблицаКоманд.Добавить();
  НоваяКоманда.Представление = Представление;
  НоваяКоманда.Идентификатор = Идентификатор;
  НоваяКоманда.Использование = Использование;
  НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
  НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

// ФУНКЦИОНАЛЬНАЯ ЧАСТЬ
Процедура  ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначения, СозданныеОбъекты, ПараметрыВыполненияКоманды) Экспорт

	Для каждого ТекущаяСсылка Из ОбъектыНазначения Цикл
		
		ДокументОПС 		= Документы.ОтчетПроизводстваЗаСмену.СоздатьДокумент();
		ДокументОПС.Дата 	= ТекущаяСсылка.Дата;
		
		Если ТипЗнч(ТекущаяСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда			
			
			ЗаполнитьОПСПоОтчетуКомиссионераОПродажах(ДокументОПС, ТекущаяСсылка);			
			
		ИначеЕсли ТипЗнч(ТекущаяСсылка) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда 
			
			ЗаполнитьОПСПоОтчетуОРозничныхПродажах(ДокументОПС, ТекущаяСсылка);
			
		КонецЕсли;
		
		ДокументОПС.Записать(РежимЗаписиДокумента.Запись);			
		
		СозданныеОбъекты.Добавить(ДокументОПС.Ссылка);
			
	КонецЦикла; 
	

КонецПроцедуры

// Заполнить "Отчет производства за смену" по "Отчет комиссионера о продажах"
//
// Параметры:
//  НовыйДокументОПС - ДокументОбъект.ОтчетПроизводстваЗаСмену	 - 
//  ТекущаяСсылка	 - ДокументСсылка.ОтчетКомиссионераОПродажах - 
//
Процедура ЗаполнитьОПСПоОтчетуКомиссионераОПродажах(НовыйДокументОПС, Знач ТекущаяСсылка)
		
	НовыйДокументОПС.Комментарий 	= ТекущаяСсылка.Комментарий;
	НовыйДокументОПС.Организация 	= ТекущаяСсылка.Организация;
	НовыйДокументОПС.Ответственный 	= ТекущаяСсылка.Ответственный;
	
	Продукция = НовыйДокументОПС.Продукция;
	
	Для Каждого ТекСтрокаУслуги Из ТекущаяСсылка.Товары Цикл
		
		НоваяСтрока = Продукция.Добавить();
		
		НоваяСтрока.Количество 				= ТекСтрокаУслуги.Количество;
		НоваяСтрока.Номенклатура 			= ТекСтрокаУслуги.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения 		= ТекСтрокаУслуги.ЕдиницаИзмерения;
		НоваяСтрока.Коэффициент				= ТекСтрокаУслуги.Коэффициент;
		НоваяСтрока.НоменклатурнаяГруппа 	= НоваяСтрока.Номенклатура.НоменклатурнаяГруппа;
		НоваяСтрока.ПлановаяСтоимость		= ТекСтрокаУслуги.СуммаПередачи;
		НоваяСтрока.СуммаПлановая 			= ТекСтрокаУслуги.СуммаПередачи;
		НоваяСтрока.Счет 					= ТекСтрокаУслуги.СчетУчетаБУ;
		
	КонецЦикла;		
	
	ЗаполнениеДокументов.Заполнить(НовыйДокументОПС, ТекущаяСсылка);
	
	Если НЕ ЗначениеЗаполнено(НовыйДокументОПС.СчетЗатрат) Тогда
		НовыйДокументОПС.СчетЗатрат   = ПланыСчетов.Хозрасчетный.ОсновноеПроизводство;
	КонецЕсли;
	
	НовыйДокументОПС.ИспользоватьМатериалы = Истина;
	
КонецПроцедуры

// Заполнить "Отчет производства за смену" по "Отчет о розничных продажах"
//
// Параметры:
//  НовыйДокументОПС - ДокументОбъект.ОтчетПроизводстваЗаСмену	 - 
//  ТекущаяСсылка	 - ДокументСсылка.ОтчетОРозничныхПродажах	 - 
//
Процедура ЗаполнитьОПСПоОтчетуОРозничныхПродажах(НовыйДокументОПС, Знач ТекущаяСсылка)
	
	НовыйДокументОПС.Комментарий 	= ТекущаяСсылка.Комментарий;
	НовыйДокументОПС.Организация 	= ТекущаяСсылка.Организация;
	НовыйДокументОПС.Ответственный 	= ТекущаяСсылка.Ответственный;
	
	Продукция = НовыйДокументОПС.Продукция;
	
	Для Каждого ТекСтрокаУслуги Из ТекущаяСсылка.Товары Цикл
		
		НоваяСтрока = Продукция.Добавить();
		
		НоваяСтрока.Количество 				= ТекСтрокаУслуги.Количество;
		НоваяСтрока.Номенклатура 			= ТекСтрокаУслуги.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения 		= ТекСтрокаУслуги.ЕдиницаИзмерения;
		НоваяСтрока.Коэффициент				= ТекСтрокаУслуги.Коэффициент;
		НоваяСтрока.НоменклатурнаяГруппа 	= НоваяСтрока.Номенклатура.НоменклатурнаяГруппа;
		НоваяСтрока.ПлановаяСтоимость		= ТекСтрокаУслуги.Сумма;
		НоваяСтрока.СуммаПлановая 			= ТекСтрокаУслуги.Сумма;
		НоваяСтрока.Счет 					= ТекСтрокаУслуги.СчетУчетаБУ;
		
	КонецЦикла;		
	
	ЗаполнениеДокументов.Заполнить(НовыйДокументОПС, ТекущаяСсылка);
	
	Если НЕ ЗначениеЗаполнено(НовыйДокументОПС.СчетЗатрат) Тогда
		НовыйДокументОПС.СчетЗатрат   = ПланыСчетов.Хозрасчетный.ОсновноеПроизводство;
	КонецЕсли;
	
	НовыйДокументОПС.ИспользоватьМатериалы = Истина;	
	
КонецПроцедуры

 